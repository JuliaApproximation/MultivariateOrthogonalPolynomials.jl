struct KronPolynomial{d, T, PP} <: MultivariateOrthogonalPolynomial{d, T}
    args::PP
end

KronPolynomial{d,T}(a::Vararg{Any,d}) where {d,T} = KronPolynomial{d,T,typeof(a)}(a)
KronPolynomial{d}(a::Vararg{Any,d}) where d = KronPolynomial{d,mapreduce(eltype, promote_type, a)}(a...)
KronPolynomial(a::Vararg{Any,d}) where d = KronPolynomial{d}(a...)
KronPolynomial{d,T}(a::AbstractVector) where {d,T} = KronPolynomial{d,T,typeof(a)}(a)
KronPolynomial{d}(a::AbstractVector) where d = KronPolynomial{d,eltype(eltype(a))}(a)
KronPolynomial(a::AbstractVector) = KronPolynomial{length(a)}(a)

function show(io::IO, P::KronPolynomial)
    for k = 1:length(P.args)
        print(io, "$(P.args[k])")
        k â‰ Â length(P.args) && print(io, " âŠ— ")
    end
end

==(A::KronPolynomial, B::KronPolynomial) = length(A.args) == length(B.args) && all(map(==, A.args, B.args))

const RectPolynomial{T, PP} = KronPolynomial{2, T, PP}



axes(P::KronPolynomial) = (Inclusion(Ã—(map(domain, axes.(P.args, 1))...)), _krontrav_axes(axes.(P.args, 2)...))
function getindex(P::RectPolynomial{T}, xy::StaticVector{2}, Jj::BlockIndex{1})::T where T
    a,b = P.args
    J,j = Int(block(Jj)),blockindex(Jj)
    x,y = xy
    a[x,J-j+1]b[y,j]
end
getindex(P::RectPolynomial, xy::StaticVector{2}, B::Block{1}) = [P[xy, B[j]] for j=1:Int(B)]
function getindex(P::RectPolynomial, xy::StaticVector{2}, JR::BlockOneTo)
    A,B = P.args
    x,y = xy
    N = size(JR,1)
    DiagTrav(A[x,OneTo(N)] .* B[y,OneTo(N)]')
end
# Actually Jxáµ€
function jacobimatrix(::Val{1}, P::RectPolynomial)
    A,B = P.args
    X = jacobimatrix(A)
    KronTrav(Eye{eltype(X)}(âˆž), X)
end
# Actually Jyáµ€
function jacobimatrix(::Val{2}, P::RectPolynomial)
    A,B = P.args
    Y = jacobimatrix(B)
    KronTrav(Y, Eye{eltype(Y)}(âˆž))
end
@simplify function *(Dx::PartialDerivative{1}, P::RectPolynomial)
    A,B = P.args
    U,M = (Derivative(axes(A,1))*A).args
    # We want I âŠ— DÂ² as A âŠ— B means B * X * A'
    RectPolynomial(U,B) * KronTrav(Eye{eltype(M)}(âˆž), M)
end

@simplify function *(Dx::PartialDerivative{2}, P::RectPolynomial)
    A,B = P.args
    U,M = (Derivative(axes(B,1))*B).args
    RectPolynomial(A,U) * KronTrav(M, Eye{eltype(M)}(âˆž))
end

function weaklaplacian(P::RectPolynomial)
    A,B = P.args
    Î”_A,Î”_B = weaklaplacian(A), weaklaplacian(B)
    M_A,M_B = grammatrix(A), grammatrix(B)
    KronTrav(Î”_A,M_B) + KronTrav(M_A,Î”_B)
end

function \(P::RectPolynomial, Q::RectPolynomial)
    PA,PB = P.args
    QA,QB = Q.args
    KronTrav(PA\QA, PB\QB)
end

@simplify function *(Ac::QuasiAdjoint{<:Any,<:RectPolynomial}, B::RectPolynomial)
    PA,PB = Ac'.args
    QA,QB = B.args
    KronTrav(PA'QA, PB'QB)
end


struct ApplyPlan{T, F, Pl}
    f::F
    plan::Pl
end

ApplyPlan(f, P) = ApplyPlan{eltype(P), typeof(f), typeof(P)}(f, P)

*(A::ApplyPlan, B::AbstractArray) = A.f(A.plan*B)

basis_axes(d::Inclusion{<:Any,<:ProductDomain}, v) = KronPolynomial(map(d -> basis(Inclusion(d)),components(d.domain))...)

struct TensorPlan{T, Plans}
    plans::Plans
end

TensorPlan(P...) = TensorPlan{mapreduce(eltype,promote_type,P), typeof(P)}(P)

function *(A::TensorPlan, B::AbstractArray)
    for p in A.plans
        B = p*B
    end
    B
end

function checkpoints(P::RectPolynomial)
    x,y = checkpoints.(P.args)
    SVector.(x, y')
end

function plan_transform(P::KronPolynomial{d,<:Any,<:Fill}, B::Tuple{Block{1}}, dims=1:1) where d
    @assert dims == 1

    T = first(P.args)
    @assert d == 2
    ApplyPlan(DiagTrav, plan_transform(T, tuple(Fill(Int(B[1]),d)...)))
end

function grid(P::RectPolynomial, B::Block{1})
    x,y = grid.(P.args, Int(B))
    SVector.(x, y')
end

function plotgrid(P::RectPolynomial, B::Block{1})
    x,y = plotgrid.(P.args, Int(B))
    SVector.(x, y')
end


function plan_transform(P::KronPolynomial{d}, B::Tuple{Block{1}}, dims=1:1) where d
    @assert dims == 1 || dims == 1:1 || dims == (1,)
    @assert d == 2
    N = Int(B[1])
    Fx,Fy = plan_transform(P.args[1], (N,N), 1),plan_transform(P.args[2], (N,N), 2)
    ApplyPlan(DiagTrav, TensorPlan(Fx,Fy))
end

applylayout(::Type{typeof(*)}, ::Lay, ::DiagTravLayout) where Lay <: AbstractBasisLayout = ExpansionLayout{Lay}()
ContinuumArrays._mul_plotgrid(::Tuple{Any,DiagTravLayout{<:PaddedLayout}}, (P,c)) = plotgrid(P, maximum(blockcolsupport(c)))

pad(C::DiagTrav, ::BlockedOneTo{Int,RangeCumsum{Int,OneToInf{Int}}}) = DiagTrav(pad(C.array, âˆž, âˆž))

QuasiArrays.mul(A::BivariateOrthogonalPolynomial, b::DiagTrav) = ApplyQuasiArray(*, A, b)

function Base.unsafe_getindex(f::Mul{MultivariateOPLayout{2},<:DiagTravLayout{<:PaddedLayout}}, ð±::SVector)
    P,c = f.A, f.B
    A,B = P.args
    x,y = ð±
    clenshaw(clenshaw(paddeddata(c.array), recurrencecoefficients(A)..., x), recurrencecoefficients(B)..., y)
end

Base.@propagate_inbounds function getindex(f::Mul{MultivariateOPLayout{2},<:DiagTravLayout{<:PaddedLayout}}, x::SVector, j...)
    @inbounds checkbounds(ApplyQuasiArray(*,f.A,f.B), x, j...)
    Base.unsafe_getindex(f, x, j...)
end

## Special Legendre case

function transform_ldiv(K::KronPolynomial{d,V,<:Fill{<:Legendre}}, f::Union{AbstractQuasiVector,AbstractQuasiMatrix}) where {d,V}
    T = KronPolynomial{d}(Fill(ChebyshevT{V}(), size(K.args)...))
    dat = (T \ f).array
    DiagTrav(pad(FastTransforms.th_cheb2leg(paddeddata(dat)), axes(dat)...))
end
